plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
    id 'org.beryx.jlink' version '2.24.1'
    id "org.sonarqube" version "3.3"
}

group 'local.uniclog'
version = '0.1'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainModule = 'local.uniclog'
    mainClass = 'local.uniclog.MainAppUi'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation 'org.controlsfx:controlsfx:11.1.1'

    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    implementation 'org.projectlombok:lombok:1.18.22'
    implementation 'org.slf4j:slf4j-simple:1.8.0-beta4'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation 'net.java.dev.jna:jna-platform:5.11.0'
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip") as RegularFile
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'app'
    }
    jpackage {
        appVersion = '0.1'
        icon = "src/main/resources/local/uniclog/img/txt_icon.ico"
        imageOptions = [
                "--vendor", "Uniclog Software",
                '--app-version', version
        ]
        installerType = 'msi'
        installerOptions = [
                '--win-dir-chooser', '--win-shortcut'
        ]
    }
}

jlinkZip {
    group = 'distribution'
}

sonarqube {
    properties {
        property "sonar.projectKey", "uniclog_Project12"
        property "sonar.organization", "uniclog"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}